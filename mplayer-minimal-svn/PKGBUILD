# Maintainer: Pierre Chapuis <catwell at archlinux dot us>

# NOTE: This package relies on Mplayer's configure script to detect what is
# useful on your system. Build it on the machine you will use it on.
# If some features don't work, install the related optdepends *before*
# building.
# Before reporting any bug, please download a fresh copy of the package from
# the AUR and build the package using makepkg (not Yaourt).

pkgname=mplayer-minimal-svn
pkgver=31880
pkgrel=1
pkgdesc='Famous multimedia player, dev. version, without its GUI'
arch=('i686' 'x86_64')
url='http://www.mplayerhq.hu/'
license=('GPL')
depends=('ttf-dejavu')
makedepends=('subversion' 'yasm')
optdepends=('freetype2: subtitles and OSD')
replaces=('mplayer-nogui-svn')
conflicts=('mplayer')
provides=("mplayer=$pkgver")
source=()
md5sums=()

_svnmod=mplayer
_svntrunk="svn://svn.mplayerhq.hu/mplayer/trunk/"

build() {

  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    cd $_svnmod
    svn up -r $pkgver
  else
    svn co $_svntrunk --config-dir . -r $pkgver $_svnmod
  fi
  msg 'SVN checkout done or server timeout'

  cd $srcdir
  mkdir -p dirty
  cp -r $_svnmod dirty
  cd dirty/$_svnmod

  msg 'Building the package...'
  unset CFLAGS LDFLAGS

  ./configure --prefix=/usr --confdir=/etc/mplayer --disable-gui \
    --disable-runtime-cpudetection --enable-largefiles --enable-menu \
    --disable-x264 --disable-gif --disable-mencoder \
    --extra-cflags=-fno-strict-aliasing

  make
  make -j1 DESTDIR=$pkgdir install

  install -m0644 etc/{codecs.conf,input.conf,example.conf} $pkgdir/etc/mplayer
  install -d $pkgdir/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf \
   $pkgdir/usr/share/mplayer/subfont.ttf

  msg 'Cleaning build files...'
  rm -rf $srcdir/dirty

}
